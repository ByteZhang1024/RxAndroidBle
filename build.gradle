apply from: 'dependencies.gradle'
apply from: 'checkstyle.gradle'

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.19.0'
        classpath 'gradle.plugin.org.codehaus.groovy:groovy-android-gradle-plugin:3.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.4'
    }
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'

    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven {
            name = "githubPackages"
            url = uri("https://maven.pkg.github.com/ByteZhang1024/RxAndroidBle")
            credentials {
                username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
    }

    buildscript {
        repositories {
            gradlePluginPortal()
            google()
            mavenCentral()
        }
    }

    configurations {
        doclava
    }

    dependencies {
        doclava 'com.google.doclava:doclava:1.0.6'
    }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }

        tasks.withType(Test) {
            useJUnitPlatform()
            testLogging {
                exceptionFormat "full"
                events "started", "skipped", "passed", "failed"
                showStandardStreams true
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

